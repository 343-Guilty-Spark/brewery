# Andy's Workshop Brewery Max31865 RTD controller ATMega328p firmware
# Copyright (c) 2017 Andy Brown. http://www.andybrown.me.uk
# Please see website for licensing terms.

"""
Usage: scons [upload]

  [upload]
    specify this option to automatically upload to
    using avrdude to a USBASP connected MCU.

To do a 'clean' use the -c flag
To do a parallel build use the -jN flag
"""

import os
import random


#
# create a random uuid if not already present
#

def createSerialNumber():

  if not os.path.exists("serialnumber.txt"):
    with open("serialnumber.txt", "w") as f:
      f.write(str(random.randrange(0,4294967295)))

  with open("serialnumber.txt", "r") as f:
    serial = f.read()

  return serial


# source the environment

env=Environment(ENV=os.environ)

# check for and create the serial number

serial = createSerialNumber()

# get the version string

VERSION=os.popen('egrep "#define\s+FIRMWARE_VERSION" FirmwareVersion.h  | cut -d \\" -f2').read()
VERSION=VERSION.rstrip()

# compiler is avr-gcc

env.Replace(CC="avr-gcc")
env.Replace(CXX="avr-g++")
env.Replace(PROGSUFFIX=".elf")

PROGRAM = "brewery-max31865-rtd-v"+VERSION

# set up our options

env.Replace(CXXFLAGS=["-mmcu=atmega328p",
                      "-Os",
                      "-g",
                      "-DF_CPU=8000000",
                      "-DBOARD_SERIAL=" + serial,
                      "-std=c++1y",
                      "-Wall",
                      "-Werror",
                      "-Wextra",
                      "-pedantic-errors",
                      "-fno-rtti",
                      "-mcall-prologues",
                      "-ffunction-sections",
                      "-fdata-sections",
                      "-fno-exceptions"])

env.Replace(CFLAGS=["-mmcu=atmega328p",
                      "-Os",
                      "-g",
                      "-DF_CPU=8000000",
                      "-DBOARD_SERIAL=" + serial,
                      "-std=c99",
                      "-Wall",
                      "-Werror",
                      "-Wextra",
                      "-pedantic-errors",
                      "-mcall-prologues",
                      "-ffunction-sections",
                      "-fdata-sections",
                      "-fno-exceptions"])

env.Replace(LINKFLAGS=["-Wl,-Map,"+PROGRAM+".map",
                       "-mrelax",
                       "-Wl,-u,vfprintf",
                       "-lprintf_flt",
                       "-lm",
                       "-Wl,--gc-sections",
                       "-mmcu=atmega328p"])

# compile source code to .elf binary

elf=env.Program(PROGRAM,[ Glob("*.cpp"), Glob("*.c") ])
Decider('timestamp-newer')

# convert elf to hex flashable image

hexfile=env.Command(PROGRAM+".hex",elf,"avr-objcopy -j .text -j .data -O ihex $SOURCE $TARGET")
env.Install("bin",hexfile)
env.Alias("install","bin")

# calculate size and generate assembly source

env.Command(PROGRAM+".siz",elf,"avr-size $SOURCE | tee $TARGET")
env.Command(PROGRAM+".lst",elf,"avr-objdump -S $SOURCE > $TARGET")

# upload target uses avrdude

flash_cmd="avrdude -c usbasp -p m328p -e -U flash:w:"+PROGRAM+".hex"
fuse_cmd="avrdude -c usbasp -p m328p -e -U lfuse:w:0xcf:m -U hfuse:w:0xd1:m"

upload=env.Alias("upload",PROGRAM+".hex",flash_cmd)
fuse=env.Alias("fuse",PROGRAM+".hex",fuse_cmd)

AlwaysBuild([upload,fuse])
